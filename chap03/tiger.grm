/*
 * SHIFT-REDUCE CONFLICTS THAT I ENCOUNTERED:
 *
 *   IF expression THEN expression
 *   (if x=1 then a = 1) + 2
 *   or
 *   (if x=1 then a = 1 + 2)
 *
 *   IF expression THEN expression ELSE expression
 *   (if x=1 then a = 1 else a = 1) + 2
 *   or
 *   (if x=1 then a = 1 else a = 1 + 2)
 *
 *   WHILE expression DO expression
 *   (while x=1 do a = 1) + 2
 *   or
 *   (while x=1 do a = 1 + 2)
 *
 *   ID LBRACK expression RBRACK OF expression
 *   (x[3] of 1) + 2
 *   or
 *   (x[3] of 1 + 2)
 *
 * References:
 *
 *   [1] http://linuxgazette.net/issue93/ramankutty.html
 *
 */

%{
#include <stdio.h>
#include "util.h"
#include "errormsg.h"

int yylex(void); /* function prototype */

void yyerror(char *s)
{
 EM_error(EM_tokPos, "%s", s);
}
%}


%union {
	int pos;
	int ival;
	string sval;
	}

%token <sval> ID STRING
%token <ival> INT

%token 
  COMMA COLON SEMICOLON LPAREN RPAREN LBRACK RBRACK 
  LBRACE RBRACE DOT 
  PLUS MINUS TIMES DIVIDE EQ NEQ LT LE GT GE
  AND OR ASSIGN
  ARRAY IF THEN ELSE WHILE FOR TO DO LET IN END OF 
  BREAK NIL
  FUNCTION VAR TYPE 

%start program

%nonassoc DO OF
%nonassoc THEN /* ELSE must come after THEN! */
%nonassoc ELSE
%left SEMICOLON
%left ASSIGN
%left OR
%left AND
%nonassoc EQ NEQ GT LT GE LE
%left PLUS MINUS
%left TIMES DIVIDE
%left UMINUS

%%

program: expression

expression
        : INT
        | STRING
        | BREAK
        | NIL
        | lvalue
        | assignment
        | selection
        | iteration
        | function_call
        | arithmetic_expression
        | comparison
        | boolean_expression
        | array_creation
        | MINUS expression %prec UMINUS
        | ID LBRACE field_assignments RBRACE
        | LPAREN RPAREN
        | LPAREN expression_sequence RPAREN
        | LET declaration_sequence IN END
        | LET declaration_sequence IN expression_sequence END

lvalue
        : ID lvalue_extension

lvalue_extension
        : /* empty */
        | DOT ID lvalue_extension
        | LBRACK expression RBRACK lvalue_extension

function_call
        : ID LPAREN RPAREN
        | ID LPAREN expression_list RPAREN

arithmetic_expression
        : expression PLUS expression
        | expression MINUS expression
        | expression TIMES expression
        | expression DIVIDE expression

comparison
        : expression EQ expression
        | expression NEQ expression
        | expression GT expression
        | expression LT expression 
        | expression GE expression 
        | expression LE expression 

boolean_expression
        : expression AND expression
        | expression OR expression

field_assignments
        : /* empty */
        | field_assignment_list

field_assignment_list
        : field_assignment
        | field_assignment_list COMMA field_assignment

field_assignment
        : ID EQ expression

array_creation
        : ID LBRACK expression RBRACK OF expression

declaration_sequence
        : /* empty */
        | declaration declaration_sequence

declaration
        : type_declaration
        | variable_declaration
        | function_declaration

type_declaration
        : TYPE ID EQ type

type
        : ID
        | LBRACE type_fields RBRACE
        | ARRAY OF ID

type_fields
        : /* empty */
        | type_field_list

type_field_list
        : ID COLON ID
        | type_field_list COMMA ID COLON ID

variable_declaration
        : VAR ID ASSIGN expression
        | VAR ID COLON ID ASSIGN expression

function_declaration
        : FUNCTION ID LPAREN type_fields RPAREN EQ expression
        | FUNCTION ID LPAREN type_fields RPAREN COLON ID EQ expression

selection
        : IF expression THEN expression ELSE expression
        | IF expression THEN expression

iteration
        : WHILE expression DO expression
        | FOR ID ASSIGN expression TO expression DO expression

expression_sequence
        : expression
        | expression_sequence SEMICOLON expression

expression_list
        : expression
        | expression_list COMMA expression

assignment
        : lvalue ASSIGN expression
